// hsp3-debug-self (HDS) proxy

#include "hspext.as"
#include "hspcmp.as"

#packopt name "hds_client_proxy"
#packopt hide 1

#define true 1
#define false 0
#define gsel_hide (-1)
#define gsel_show 1
#define mesbox_readonly 0
#define objmode_font 2
#define s_window_id_default 0

	gsel s_window_id_default, gsel_hide

	s_src_name = "hds-client\\hds_client.hsp"
	s_obj_name = "hds_client.ax"
	s_runtime_name = "hsp3utf.exe"
	s_debug_mode = 0
	s_exit_code = 1

	sdim s_error, 0x10000
	sdim s_buffer
	sdim s_line

	onexit goto *l_on_exit

	gosub *l_compile
	if stat == false {
		goto *l_fail
	}

	gosub *l_run
	if stat == false {
		goto *l_fail
	}

	s_exit_code = 0
	end

*l_compile
	hsc_ini s_src_name
	hsc_objname s_obj_name
	hsc_comp 0 // compile
	if stat {
		hsc_getmes s_error
		return false
	}

	hsc3_getruntime s_runtime_name, s_obj_name
	if s_runtime_name == "" {
		s_runtime_name = "hsp3.exe"
	}
	return true

*l_run
	s_cmd = "\"" + dir_exe + "\\" + s_runtime_name + "\" " + s_obj_name + " " + dir_cmdline
	pipeexec s_buffer, s_cmd, true // true = show window
	if stat {
		s_error = "Could not execute: '" + s_cmd + "'"
		return false
	}

	repeat
		pipeget s_line
		if stat == 0 {
			break
		}
		await 100
	loop
	return true

*l_on_exit

	end s_exit_code

*l_fail

	title "hds_proxy"

	font "MS Gothic", 12
	objmode objmode_font
	mesbox s_error, ginfo_winx, ginfo_winy, mesbox_readonly

	gsel s_window_id_default, gsel_show
	stop
