; Based on sample/misc/mkexe.hsp
; LICENSE: CC0-1.0
; VERSION: v0.3.0

#runtime "hsp3cl"

#packopt name "bootstrap"

#include "hspcmp.as"

#define k_dir_sep "\\"

; (hsc_comp p1)
; 4: utf-8 input
#define k_compile_opts 4

; (hsc_comp p2
; (4: packfile, 32: utf-8 output)
#define k_pp_opts (4 | 32)

	hsp3_root = dir_exe
	bootstrap_dir = dir_cur
	src_dir = getpath(bootstrap_dir, 16 + 32) + "src"
	src_name = "ginger_main_cli.hsp"
	success = 0

	; Change the current directory to `../src`.
	chdir src_dir

	; Preparation:
	hsc_ini src_name
	if stat : goto *l_fail

	hsc_objname "start.ax"
	if stat : goto *l_fail

	hsc_compath hsp3_root + "\\common\\"
	if stat : goto *l_fail

	; Compile. `start.ax` will be created.
	hsc_comp k_compile_opts, k_pp_opts, 0
	if stat : goto *l_fail

	; Make an executable.
	hsc3_make hsp3_root + "\\" + src_name + ".dpm"
	if stat : goto *l_fail

	chdir bootstrap_dir
	end

*l_fail

	; Get the buffer size.
	s_message_size = 0
	hsc3_messize s_message_size
	sdim s_error, s_message_size + 1

	; Get the output message from the compiler.
	hsc_getmes s_error
	mes s_error

	chdir bootstrap_dir
	end 1
