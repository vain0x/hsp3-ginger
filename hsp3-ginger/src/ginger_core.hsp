; メイン処理

#include "ginger_constants.hsp"
#include "ginger_args.hsp"
#include "ginger_compile.hsp"
#include "ginger_sub_build.hsp"
#include "ginger_sub_dotfiles.hsp"
#include "ginger_sub_help.hsp"
#include "ginger_sub_run.hsp"
#include "mod_cmdline_parser.hsp"
#include "mod_unicode_cnv.hsp"

#module m_ginger_core

#define true 1
#define false 0

#deffunc ginger_core_main

	sdim s_error

	sdim s_forward_args
	s_forward_argc = 0

	ginger_args_parse s_forward_args, s_forward_argc

	ginger_core_do_subcommand
	if stat == false {
		ginger_infra_error s_error
		end EXIT_FAILURE
	}

	assert s_error == ""
	end

; サブコマンドを実行する
;
; 成功したら stat=true
#deffunc ginger_core_do_subcommand \
	local subcommand, local show_help, local version

	ginger_args_get_subcommand subcommand
	if stat == false {
		subcommand = "help"
	}

	ginger_args_show_help
	show_help = stat

	if subcommand == "help" {
		ginger_sub_help_main s_forward_args, s_forward_argc, show_help, s_error
		return stat
	}

	if subcommand == "version" {
		ginger_infra_get_version version
		ginger_infra_stdout version
		return stat
	}

	if subcommand == "run" {
		ginger_sub_run_main s_forward_args, s_forward_argc, show_help, s_error
		return stat
	}

	if subcommand == "build" {
		ginger_sub_build_main s_forward_args, s_forward_argc, show_help, s_error
		return stat
	}

	if subcommand == "dotfiles" {
		ginger_sub_dotfiles_main s_forward_args, s_forward_argc, show_help, s_error
		return stat
	}

	s_error = "不明なサブコマンド: " + subcommand
	return false

#global
